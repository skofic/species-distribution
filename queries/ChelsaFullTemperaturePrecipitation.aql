/**
 * Generate Chelsa full resolution temperature/precipitation dump.
 *
 * Group all chelsa records by temperature and precipitation pair.
 *
 * Expects the following bind variables:
 * @@collectionStats: 'Stats' General statistics.
 * @@collectionChelsa: 'Chelsa' The Chelsa collection.
 */
LET stat_chelsa = (
    FOR stat IN @@collectionStats
        FILTER stat._key == "Chelsa"
    RETURN stat
)[0]

LET tas_chelsa_max = stat_chelsa.env_climate_tas.max
LET tas_chelsa_min = stat_chelsa.env_climate_tas.min

LET pr_chelsa_max = stat_chelsa.env_climate_pr.max
LET pr_chelsa_min = stat_chelsa.env_climate_pr.min

LET tas_chelsa_range = tas_chelsa_max - tas_chelsa_min
LET pr_chelsa_range = pr_chelsa_max - pr_chelsa_min

FOR doc IN @@collectionChelsa

    COLLECT mean_annual_temp = doc.properties["1981-2010"].env_climate_tas,
            mean_annual_prc = doc.properties["1981-2010"].env_climate_pr
    WITH COUNT INTO items

RETURN {
    _key: MD5(TO_STRING([mean_annual_temp, mean_annual_prc])),
    count: items,
    properties: {
        env_climate_tas: mean_annual_temp,
        env_climate_pr: mean_annual_prc
    },
    scale: {
        env_climate_tas_ratio: (mean_annual_temp - tas_chelsa_min) / tas_chelsa_range,
        env_climate_pr_ratio: (mean_annual_prc - pr_chelsa_min) / pr_chelsa_range
    }
}