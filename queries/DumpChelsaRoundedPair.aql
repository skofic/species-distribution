/**
 * Load rounded pair into collection.
 *
 * Multiply and divide each indicator by the provided coefficient and save result in rounded collection.
 *
 * Expects the following bind variables:
 * @indicatorX: X indicator name.
 * @indicatorY: Y indicator name.
 * @intervalX: Values interval for indicator X.
 * @intervalY: Values interval for indicator Y.
 * @@collectionFULL: Collection containing full selection.
 *
 * Provide 0 as the interval value to ignore the interval.
 */
FOR doc IN @@collectionFULL

  COLLECT valueX = @intervalX == 0 ? doc.properties.@indicatorX : ROUND(doc.properties.@indicatorX / @intervalX) * @intervalX,
          valueY = @intervalY == 0 ? doc.properties.@indicatorY : ROUND(doc.properties.@indicatorY / @intervalY) * @intervalY
  AGGREGATE items = SUM(doc.count)

RETURN {
    _key: MD5(TO_STRING([valueX, valueY])),
    count: items,
    properties: {
        @indicatorX: valueX,
        @indicatorY: valueY
    }
}
