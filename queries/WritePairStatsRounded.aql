
/**
 * Set Chelsa indicators pair statistics.
 *
 * Set statistics and intervals for provided rounded indicators
 *
 * Expects the following bind variables:
 * @key: The record _key.
 * @indicatorX: First indicator name in pair.
 * @indicatorY: Second indicator name in pair.
 * @intervalX: Values rounded interval for indicator X.
 * @intervalY: Values rounded interval for indicator Y.
 * @@collectionPair: Current pair collection.
 * @@collectionStats: 'Stats' General statistics.
 */
LET items = (
	FOR doc IN @@collectionPair
		COLLECT WITH COUNT INTO length
	RETURN length
)[0]

LET elements = (
	FOR doc IN @@collectionPair
		COLLECT AGGREGATE max = MAX(doc.count)
	RETURN max
)[0]

LET indicatorXcount = LENGTH(
	FOR doc IN @@collectionPair
		COLLECT element = doc.properties.@indicatorX
	RETURN element
)

LET indicatorXweights = (
	FOR doc IN @@collectionPair
		COLLECT AGGREGATE min = MIN(doc.properties.@indicatorX),
		                  avg = AVG(doc.properties.@indicatorX),
						  max = MAX(doc.properties.@indicatorX)
	RETURN { min, avg, max }
)[0]

LET indicatorYcount = LENGTH(
		FOR doc IN @@collectionPair
			COLLECT element = doc.properties.@indicatorY
		RETURN element
)

LET indicatorYweights = (
	FOR doc IN @@collectionPair
		COLLECT AGGREGATE min = MIN(doc.properties.@indicatorY),
		                  avg = AVG(doc.properties.@indicatorY),
						  max = MAX(doc.properties.@indicatorY)
	RETURN { min, avg, max }
)[0]

UPSERT { _key: @key }
INSERT {
	_key: @key,
	items: {
		records: items,
		maxElements: elements
	},
	@indicatorX: {
	  count: indicatorXcount,
	  interval: @intervalX,
	  weights: indicatorXweights
	},
	@indicatorY: {
	  count: indicatorYcount,
      interval: @intervalY,
	  weights: indicatorYweights
	}
}
REPLACE {
	items: {
		records: items,
		maxElements: elements
	},
	@indicatorX: {
	  count: indicatorXcount,
	  interval: @intervalX,
	  weights: indicatorXweights
	},
	@indicatorY: {
	  count: indicatorYcount,
      interval: @intervalY,
	  weights: indicatorYweights
	}
}
IN @@collectionStats
OPTIONS {
    waitForSync: true,
    mergeObjects: false
}
